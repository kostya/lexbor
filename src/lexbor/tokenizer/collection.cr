require "./token"
require "../utils/paged_array"

# Helper tokenizer state class which store tokens into array,
#   provide methods to iterator throuht them.
class Lexbor::Tokenizer::Collection < Lexbor::Tokenizer::State
  getter storage

  def parse(str, ws = true)
    super(str, ws)
  end

  def initialize(page_size = 1000)
    @storage = Lexbor::Utils::PagedArray(Lexbor::Lib::HtmlToken).new(page_size)
  end

  def on_token(token)
    @storage << token.raw_token
  end

  def clear
  end

  def size
    @storage.elements_size
  end

  def root
    raise Lexbor::EmptyNodeError.new("empty collection") if size == 0
    token_pos @storage.first
  end

  def first
    raise Lexbor::EmptyNodeError.new("empty collection") if size == 0
    token_pos @storage.first
  end

  def last
    raise Lexbor::EmptyNodeError.new("empty collection") if size == 0
    token_pos @storage.last
  end

  def each
    @storage.each do |data|
      yield(token_pos(data))
    end
  end

  @[AlwaysInline]
  private def token(v : Lexbor::Lib::HtmlToken)
    Lexbor::Tokenizer::Token.new(self, v)
  end

  @[AlwaysInline]
  protected def token_pos(v : Lexbor::Utils::PagedArray::Pos(Lexbor::Lib::HtmlToken))
    TokenPos.new(self, token(v.value), v)
  end

  record TokenPos, collection : Collection, token : Lexbor::Tokenizer::Token, pos : Lexbor::Utils::PagedArray::Pos(Lexbor::Lib::HtmlToken) do
    forward_missing_to @token

    def next
      if v = @pos.next
        collection.token_pos v
      end
    end

    def prev
      if v = @pos.prev
        collection.token_pos v
      end
    end

    def right
      RightIterator.new(self)
    end

    def left
      LeftIterator.new(self)
    end

    def scope
      ScopeIterator.new(self)
    end
  end

  def inspect(io)
    io << "Lexbor::Tokenizer::Collection(0x"
    io << object_id.to_s(16)
    io << ", tokens: "
    io << @storage.elements_size
    io << ')'
  end

  module Filter
    def text_nodes
      self.select { |t| t.tag_id == Lexbor::Lib::TagIdT::LXB_TAG__TEXT }
    end

    def nodes(tag_id : Lexbor::Lib::TagIdT, opened = true)
      self.select { |t| t.tag_id == tag_id && opened != t.closed? }
    end

    def nodes(tag_sym : Symbol, opened = true)
      nodes(Utils::TagConverter.sym_to_id(tag_sym), opened)
    end

    def nodes(tag_str : String, opened = true)
      nodes(Utils::TagConverter.string_to_id(tag_str), opened)
    end
  end

  struct RightIterator
    include ::Iterator(TokenPos)
    include Lexbor::Tokenizer::Collection::Filter

    @start_node : TokenPos
    @current_node : TokenPos? = nil

    def initialize(@start_node)
      rewind
    end

    def next
      @current_node = @current_node.not_nil!.next
      @current_node || stop
    end

    def rewind
      @current_node = @start_node
    end
  end

  struct LeftIterator
    include ::Iterator(TokenPos)
    include Lexbor::Tokenizer::Collection::Filter

    @start_node : TokenPos
    @current_node : TokenPos? = nil

    def initialize(@start_node)
      rewind
    end

    def next
      cn = @current_node = @current_node.not_nil!.prev
      case cn
      when nil
        stop
      else
        cn
      end
    end

    def rewind
      @current_node = @start_node
    end
  end

  class ScopeIterator
    include ::Iterator(TokenPos)
    include Lexbor::Tokenizer::Collection::Filter

    @start_node : TokenPos
    getter current_node : TokenPos
    @stop_tag_id : Lexbor::Lib::TagIdT

    def initialize(@start_node)
      @current_node = @start_node
      @stop_tag_id = @start_node.tag_id
    end

    def next
      nxt = @current_node.next
      if nxt && !((nxt.tag_id == @stop_tag_id) && nxt.closed?)
        @current_node = nxt
      else
        @current_node = nxt if nxt
        stop
      end
    end

    def rewind
      @current_node = @start_node
      @stop_tag_id = @start_node.tag_id
    end
  end
end
